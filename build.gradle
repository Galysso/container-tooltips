import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
	id 'fabric-loom' version '1.9.1'
	id 'maven-publish'
  	id "org.jetbrains.kotlin.jvm" version "2.0.0"
	id 'com.modrinth.minotaur' version '2.+'
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

loom {
    splitEnvironmentSourceSets()

	mods {
		"container-tooltips" {
			sourceSet sourceSets.main
			sourceSet sourceSets.client
		}
	}

}

repositories {
	maven { url "https://maven.shedaniel.me/" }
	maven { url "https://maven.terraformersmc.com/releases/" }
	maven { url = "https://cursemaven.com" }
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
	modImplementation "net.fabricmc:fabric-language-kotlin:${project.fabric_kotlin_version}"

	modApi("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}") {
		exclude(group: "net.fabricmc.fabric-api")
	}
	modApi("com.terraformersmc:modmenu:${project.mod_menu_version}")

	// Legendary Tooltips
	modImplementation("curse.maven:legendary-tooltips-fabric-542478:6400658") {
	    exclude group: "net.minecraft"
	}

	// Iceberg
	modImplementation("curse.maven:iceberg-fabric-539382:6379428") {
		exclude group: "net.minecraft"
	}

	// Prism
	modImplementation("curse.maven:prism-fabric-665526:6372978") {
	    exclude group: "net.minecraft"
	}
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

tasks.withType(KotlinCompile).configureEach {
	compilerOptions {
		jvmTarget = JvmTarget.JVM_21
	}
}

java {
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
}

modrinth {
	token = System.getenv("MODRINTH_TOKEN")
	projectId = "bec9o8lo"
	versionNumber = "${project.version}"
	versionType = "release"
	changelog = file("changelog/CHANGELOG-${project.version}.md").exists() ? file("changelog/CHANGELOG-${project.version}.md").text : ""
	uploadFile = remapJar
	gameVersions = ["1.21", "1.21.1"]
	loaders = ["fabric"]
	dependencies {
		required.project 'P7dR8mSH' // fabric-api
		required.project 'Ha28R6CL' // fabric-language-kotlin
		optional.project 'mOgUt4GM' // modmenu
		embedded.project '9s6osm5g' // cloth-config-api
	}
}
